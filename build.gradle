buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id 'com.github.ben-manes.versions' version '0.21.0'
    id "com.jfrog.bintray" version "1.8.1"
    id "org.sonarqube" version "2.7"
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC14"
}

repositories {
    jcenter()
}

detekt {
    input = files(rootProject.rootDir)
    buildUponDefaultConfig = true
}

allprojects {
    group 'io.gitlab.arturbosch'
    version kshVersion
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        testCompile "junit:junit:$junitVersion"
        testCompile "com.willowtreeapps.assertk:assertk:$assertkVersion"
        testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
        testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
        testCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    bintray {
        user = System.getenv("BINTRAY_USER") ?: ""
        key = System.getenv("BINTRAY_API_KEY") ?: ""
        publications = ["ksh"]

        pkg {
            repo = 'generic'
            name = 'ksh'
            userOrg = 'arturbosch'
            licenses = ['Apache-2.0']
            vcsUrl = "https://github.com/arturbosch/ksh"

            version {
                name = project.version
                released = new Date()
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            ksh(MavenPublication) {
                artifact sourcesJar
                from components.java
                groupId project.group
                artifactId project.name
                version project.version
            }
        }
    }
}
