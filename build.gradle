buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id 'com.github.ben-manes.versions' version '0.27.0'
    id "com.jfrog.bintray" version "1.8.4" apply false
    id "org.sonarqube" version "2.8"
    id "io.gitlab.arturbosch.detekt" version "1.4.0"
}

repositories {
    jcenter()
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detektVersion"
}

detekt {
    input = files(rootProject.rootDir)
    buildUponDefaultConfig = true
    failFast = true
    baseline = file("$rootDir/baseline.xml")
}

allprojects {
    group 'io.gitlab.arturbosch'
    version kshVersion
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "http://dl.bintray.com/arturbosch/generic" }
    }

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        testImplementation "junit:junit:$junitVersion"
        testImplementation "com.willowtreeapps.assertk:assertk:$assertkVersion"
        testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
        testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
        testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
        kotlinOptions.freeCompilerArgs = ["-Xjvm-default=enable"]
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
        kotlinOptions.freeCompilerArgs = ["-Xjvm-default=enable"]
    }

    bintray {
        user = System.getenv("BINTRAY_USER") ?: ""
        key = System.getenv("BINTRAY_API_KEY") ?: ""
        publications = ["ksh"]

        pkg {
            repo = 'generic'
            name = 'ksh'
            userOrg = 'arturbosch'
            licenses = ['Apache-2.0']
            vcsUrl = "https://github.com/arturbosch/ksh"

            version {
                name = project.version
                released = new Date()
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        getArchiveClassifier().set('sources')
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            ksh(MavenPublication) {
                artifact sourcesJar
                from components.java
                groupId project.group
                artifactId project.name
                version project.version
            }
        }
    }
}
