buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
	}
}

plugins {
	id "com.github.johnrengelman.shadow" version "2.0.4"
	id 'com.github.ben-manes.versions' version '0.20.0'
	id "com.jfrog.bintray" version "1.8.3"
}

apply plugin: 'application'

mainClassName = 'io.gitlab.arturbosch.ksh.MainKt'

dependencies {
	compile project(":ksh-api")
	compile "org.jline:jline:$jlineVersion"
}

allprojects {
	group 'io.gitlab.arturbosch.ksh'
	version "$kshVersion"

	apply plugin: 'kotlin'
	apply plugin: 'com.jfrog.bintray'
	apply plugin: 'maven-publish'

	repositories {
		mavenCentral()
		mavenLocal()
	}

	dependencies {
		compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
		testCompile "junit:junit:$junitVersion"
		testCompile "com.willowtreeapps.assertk:assertk:$assertkVersion"
		testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
		testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
		testCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
	}

	compileKotlin {
		kotlinOptions.jvmTarget = "1.8"
	}

	compileTestKotlin {
		kotlinOptions.jvmTarget = "1.8"
	}

	bintray {
		user = System.getenv("BINTRAY_USER") ?: ""
		key = System.getenv("BINTRAY_API_KEY") ?: ""
		publications = ["ksh"]

		pkg {
			repo = 'generic'
			name = 'ksh'
			userOrg = 'arturbosch'
			licenses = ['Apache-2.0']
			vcsUrl = "https://github.com/arturbosch/ksh"

			version {
				name = project.version
				released = new Date()
			}
		}
	}

	publishing {
		publications {
			ksh(MavenPublication) {
				from components.java
				groupId project.group
				artifactId project.name
				version project.version
			}
		}
	}
}
